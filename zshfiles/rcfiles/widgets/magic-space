# vim: filetype=zsh

# magic-space changes behavior based on LBUFFER

__fasd_fzy() {
	local dir="$(fasd_cd -d | sort -n -r | awk '{print $2}' | fzy)"
	LBUFFER="cd $dir"
	zle reset-prompt
	zle accept-line
}

__cd_find_fzy() {
	local dir="$(find . -type d | fzy)"
	LBUFFER="cd $dir"
	zle reset-prompt
	zle accept-line
}

__git_checkout_fzy() {
	local branch=$(git branch -a | fzy | xargs) # xargs trims leading/trailing spaces
	if [ -z "$branch" ]; then
		zle reset-prompt
		return
	fi

	LBUFFER="git checkout ${branch}"
	zle reset-prompt
	zle accept-line
}

__vim_fzy() {
	local file=$(find . | fzy)
	if [ -z "$file" ]; then
		zle reset-prompt
		return
	fi

	LBUFFER="v ${file}"
	zle reset-prompt
	zle accept-line
}

case "${LBUFFER}" in
	j)
		__fasd_fzy
		;;
	jf)
		__cd_find_fzy
		;;
	gbs)
		# "g"it "b"ranch" "s"elect
		__git_checkout_fzy
		;;
	vf)
		__vim_fzy
		;;
	*)
		zle self-insert
esac
