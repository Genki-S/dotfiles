# vim: filetype=zsh

# magic-space changes behavior based on LBUFFER

__cd_fzy() {
	# combine output from fasd_cd and find, then select with fzy
	local fasd_dirs="$(fasd_cd -d | sort -n -r | awk '{print $2}')"
	local find_dirs
	find_dirs="$(timeout 0.1s find . -maxdepth 5 -type d)"
	if [ $? -ne 0 ]; then
		echo "" 1>&2
		echo "WARN: find timed out" 1>&2
	fi
	local dir="$({ echo $fasd_dirs; echo $find_dirs } | fzy)"
	___zle_cd "$dir"
}

__ghq_cd_fzy() {
	local dir="$(ghq list --full-path | fzy)"
	___zle_cd "$dir"
}

__inner_cd_fzy() {
	git_root="$(git rev-parse --show-toplevel)"
	local dir
	if [ -f "$git_root/.gitignore" ]; then
		dir="$(find "$git_root" -mindepth 1 -maxdepth 5 \
			-name '.git' -prune -o -type d -print \
			| git -C "$git_root" check-ignore --no-index -v -n --stdin 2>/dev/null \
			| sed -n 's/^::\t//p' \
			| sed "s!^$git_root/!!" \
			| fzy)"
	else
		dir="$(find "$git_root" -mindepth 1 -maxdepth 5 \
			-name '.git' -prune -o -type d \
			| sed "s!^$git_root/!!" \
			| fzy)"
	fi
	___zle_cd "$git_root/$dir"
}

___zle_cd() {
	local dir="$1"
	if [ -z "$dir" ]; then
		LBUFFER=""
		zle reset-prompt
		return
	fi
	LBUFFER="cd $dir"
	zle reset-prompt
	zle accept-line
}

__git_checkout_fzy() {
	local branch=$(git branch -a | fzy | xargs) # xargs trims leading/trailing spaces
	if [ -z "$branch" ]; then
		zle reset-prompt
		return
	fi

	LBUFFER="git checkout ${branch}"
	zle reset-prompt
	zle accept-line
}

__vim_fzy() {
	local file=$(find . -maxdepth 5 -type f | fzy)
	if [ -z "$file" ]; then
		zle reset-prompt
		return
	fi

	LBUFFER="v ${file}"
	zle reset-prompt
	zle accept-line
}

case "${LBUFFER}" in
	# "j" for "j"ump
	j)
		__cd_fzy
		;;
	# "g"hq "j"ump
	gj)
		__ghq_cd_fzy
		;;
	# "i"n-project "j"ump
	ij)
		__inner_cd_fzy
		;;
	# "g"it "b"ranch" "s"elect
	gbs)
		__git_checkout_fzy
		;;
	vf)
		__vim_fzy
		;;
	*)
		zle self-insert
esac
