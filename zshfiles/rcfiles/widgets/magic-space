# vim: filetype=zsh

# magic-space changes behavior based on LBUFFER

__cd_fzy() {
	# combine output from fasd_cd and find, then select with fzy
	local dir="$({ fasd_cd -d | sort -n -r | awk '{print $2}' ; find . -maxdepth 5 -type d ; } 2> /dev/null | fzy)"
	if [ -z "$dir" ]; then
		LBUFFER=""
		zle reset-prompt
		return
	fi
	LBUFFER="cd $dir"
	zle reset-prompt
	zle accept-line
}

__git_checkout_fzy() {
	local branch=$(git branch -a | fzy | xargs) # xargs trims leading/trailing spaces
	if [ -z "$branch" ]; then
		zle reset-prompt
		return
	fi

	LBUFFER="git checkout ${branch}"
	zle reset-prompt
	zle accept-line
}

__vim_fzy() {
	local file=$(find . | fzy)
	if [ -z "$file" ]; then
		zle reset-prompt
		return
	fi

	LBUFFER="v ${file}"
	zle reset-prompt
	zle accept-line
}

case "${LBUFFER}" in
	# "j" for "j"ump
	j)
		__cd_fzy
		;;
	gbs)
		# "g"it "b"ranch" "s"elect
		__git_checkout_fzy
		;;
	vf)
		__vim_fzy
		;;
	*)
		zle self-insert
esac
