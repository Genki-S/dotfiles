# vim: filetype=zsh

# magic-leader triggers useful things with leader key
# only works when LBUFFER is empty (i.e. when leader is first entered)

if [[ -n "$LBUFFER" ]]; then
  zle self-insert
  return
fi

__kubernetes_switch_context() {
  local contexts="$(kubectl config get-contexts | tail -n +2 | sed 's/^\*//' | sed 's/^\s*//' | sed 's/\s.*$//')"
  local selected="$(echo $contexts | fzy)"
  if [ -z "$selected" ]; then
    zle reset-prompt
    return
  fi
  LBUFFER="kubectl config use-context $selected"
  zle reset-prompt
  zle accept-line
}

__kubernetes_switch_namespace() {
  # TODO: show this on minibuffer
  LBUFFER="# collecting namespaces..."
  zle reset-prompt
  local namespaces="$(kubectl get namespaces | tail -n +2 | awk '{ print $1 }')"
  LBUFFER=""
  local selected="$(echo $namespaces | fzy)"
  if [ -z "$selected" ]; then
    zle reset-prompt
    return
  fi
  # LBUFFER="kubectl config set-context --current --namespace $selected"
  LBUFFER="kns $selected"
  zle reset-prompt
  zle accept-line
}

__aws_switch_region() {
  local regions=("ap-northeast-1" "ap-northeast-2" "ap-northeast-3" "ap-south-1" "ap-southeast-1" "ap-southeast-2")
  regions+=("eu-central-1" "eu-north-1" "eu-west-1" "eu-west-2" "eu-west-3") 
  regions+=("us-east-1" "us-east-2" "us-west-1" "us-west-2")
  regions+=("ca-central-1" "sa-east-1")

  zle reset-prompt
  LBUFFER=""
  local selected="$(echo ${(F)regions} | fzy)"
  if [ -z "$selected" ]; then
    zle reset-prompt
    return
  fi
  # LBUFFER="kubectl config set-context --current --namespace $selected"
  LBUFFER="export AWS_REGION=$selected"
  zle reset-prompt
  zle accept-line
}

__aws_switch_profile() {
  # TODO: show this on minibuffer
  LBUFFER="# collecting aws profiles..."
  zle reset-prompt
  local profiles="$(aws configure list-profiles)"
  LBUFFER=""
  local selected="$(echo $profiles | fzy)"
  if [ -z "$selected" ]; then
    zle reset-prompt
    return
  fi
  LBUFFER="export AWS_PROFILE=$selected"
  zle reset-prompt
  zle accept-line
}

local REPLY
read-from-minibuffer -k 1 "magic-leader: "
case "${REPLY}" in
  k)
    read-from-minibuffer -k 1 "k8s ([c]ontext, [n]amespace): "
    case "${REPLY}" in
      c)
        __kubernetes_switch_context
        ;;
      n)
        __kubernetes_switch_namespace
        ;;
    esac
    ;;
  a)
    read-from-minibuffer -k 1 "aws ([r]egion, [p]rofile): "
    case "${REPLY}" in
      r)
        __aws_switch_region
        ;;
      p)
        __aws_switch_profile
        ;;
    esac
    ;;
  *)
    zle -M "magic-leader: no action registered for key '$REPLY'"
esac
